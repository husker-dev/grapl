plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

apply from: addon(addons_mavenPublish)
apply from: addon(addons_moduleInfo)

sourceSets {
    main.java.srcDirs = ['kotlin']
    test.java.srcDirs = ['test']
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    api rootProject.project("modules:core")

    silentApi "$mvn_groupId:$projectName-natives-gl-win:$version"
    silentApi "$mvn_groupId:$projectName-natives-gl-linux:$version"
    silentApi "$mvn_groupId:$projectName-natives-gl-macos:$version"

    // Testing
    testImplementation subprojects
    testImplementation rootProject.project("modules:core:native")
    testImplementation rootProject.project("modules:ext-display")

    testImplementation platform("org.lwjgl:lwjgl-bom:3.3.3")
    for(module in ["", "-opengl"]){
        for(os in ["windows", "macos", "linux"]){
            for(arch in ["-arm64", ""]){
                testImplementation "org.lwjgl:lwjgl${module}"
                testRuntimeOnly "org.lwjgl:lwjgl${module}::natives-${os}${arch}"
            }
        }
    }
}

maven {
    name = artifactId = "$projectName-gl"
    description = "$projectName gl module"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

moduleInfo {
    name = "${projectName}.gl"

    requiresTransitive = [
            "grapl"
    ]
    exports = [
            "com.huskerdev.grapl.gl",
            "com.huskerdev.grapl.gl.platforms.linux.egl",
            "com.huskerdev.grapl.gl.platforms.linux.glx",
            "com.huskerdev.grapl.gl.platforms.win",
            "com.huskerdev.grapl.gl.platforms.macos",
    ]
}